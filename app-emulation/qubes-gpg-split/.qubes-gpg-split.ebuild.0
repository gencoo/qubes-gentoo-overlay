# Maintainer: Frédéric Pierret <frederic.pierret@qubes-os.org>

EAPI=7

PYTHON_COMPAT=( python3_{8..11} )

inherit eutils multilib distutils-r1 qubes

KEYWORDS="amd64"
DESCRIPTION="The Qubes service for secure gpg separation"
HOMEPAGE="http://www.qubes-os.org"
LICENSE="GPLv2"

SLOT="0"
IUSE="pandoc-bin"

DEPEND="app-emulation/qubes-libvchan-xen
        pandoc-bin? (
            app-text/pandoc-bin
        )
        !pandoc-bin? (
            app-text/pandoc
        )
        ${PYTHON_DEPS}
        "
RDEPEND="${DEPEND}"
PDEPEND=""

src_prepare() {
    [[ ${PV} == *9999 ]] && qubes_verify_sources_git "${EGIT_COMMIT}"
    default
    eapply "${FILESDIR}"/gpg-client-pipe-cat-is-built-under-lib64.patch
}

src_compile() {
    # Remove related /var/run
    sed -i 's|/etc/tmpfiles\.d/|/usr/lib/tmpfiles.d/|g' Makefile
    sed -i '/^.*\/var\/run\/.*$/d' Makefile
    # Ensure qubes.Gpg service script will use the correct path
    sed -i "s|/usr/lib/qubes-gpg-split|/usr/$(get_libdir)/qubes-gpg-split|" qubes.Gpg.service

    myopt="${myopt} DESTDIR=${D} SYSTEMD=1 BACKEND_VMM=xen LIBDIR=/usr/$(get_libdir)"
	emake ${myopt} build
}

src_install() {
	emake ${myopt} install-vm
}
